#!/usr/bin/awk -f

# TODO
# - Repeatable same checks
# - --running-shell instead of commands
#   - or running executable file?
# - Output match trailing newline
# - Output verification by command
# - Providing standard input to command
# - Decide whether go for gawk only or consider compatibility with other impls
#   - https://en.wikipedia.org/wiki/AWK#Versions_and_implementations
#   - shebang broken on mawk (debian/ubuntu default impl, gawk easily available)
# - Add suite support
#   - Run whole executable / fail first assert
#   - Group asserts by test name
# - Add support for diagnosing command to be run when the test has failed
# - Rethink how to bring colorized diffs back
#   - busybox have no '--color' for diff, so does old coreutils impl (centos)
#     - Fallback will break tests expecting colors

function usage() {
  print "Usage: assert [OPTIONS]" > "/dev/stderr"
  print "" > "/dev/stderr"
  print "Command specification:" > "/dev/stderr"
  print "    --running EXEC-LIST      Command arguments to run" > "/dev/stderr"
  print "    --forward-in             Forward stdin from assert invocation into --running command" > "/dev/stderr"
  print "" > "/dev/stderr"
  print "Validation:" > "/dev/stderr"
  print "    --exit-with N            Expected exit code" > "/dev/stderr"
  print "    --no-out                 No stdout expected" > "/dev/stderr"
  print "    --no-err                 No stderr expected" > "/dev/stderr"
  print "    --out-equals S           Stdout equal to string" > "/dev/stderr"
  print "    --err-equals S           Stderr equal to string" > "/dev/stderr"
  print "    --out-equals-file F      Stdout equal to file content" > "/dev/stderr"
  print "    --err-equals-file F      Stderr equal to file content" > "/dev/stderr"
  print "    --out-matches REGEX      Stdout matches regex" > "/dev/stderr"
  print "    --err-matches REGEX      Stderr matches regex" > "/dev/stderr"
  print "    --succeeds               Exit with 0 code without stderr" > "/dev/stderr"
  print "    --succeeds-silently      Exit with 0 code without stderr or stdout" > "/dev/stderr"
  print "" > "/dev/stderr"
  print "Other options:" > "/dev/stderr"
  print "    --message DESCRIPTION    Test description" > "/dev/stderr"
  exit EXIT_USAGE
}

# Populate config argument with tool configuration
function init_opts(config, argc, argv) {
  if (argc == 1) {
    print "Verify outcome of command run" > "/dev/stderr"
    print "" > "/dev/stderr"
    usage()
  }

  # Explicitly use empty stdin not to inherit the one from assert itself
  # Otherwise input reading command will halt the execution leaving no clue what is wrong
  config[CNF_FORWARD_IN] = ": | "

  for (i = 1; i < argc; i++) {
    switch (argv[i]) {
      case "--message":
        read_opt_value(config, argv, ++i, CNF_MESSAGE, "--message")
      break;
      case "--exit-with":
        read_opt_value(config, argv, ++i, CNF_EXIT, "--exit-with")
      break;
      case "--succeeds":
        config[CNF_EXIT] = 0
        config[CNF_STDERR_EQUALS] = ""
      break;
      case "--succeeds-silently":
        config[CNF_EXIT] = 0
        config[CNF_STDOUT_EQUALS] = ""
        config[CNF_STDERR_EQUALS] = ""
      break;
      case "--out-equals":
        read_opt_value(config, argv, ++i, CNF_STDOUT_EQUALS, "--out-equals")
      break;
      case "--out-equals-file":
        read_opt_value(config, argv, ++i, CNF_STDOUT_EQUALS_FILE, "--out-equals-file")
      break;
      case "--out-matches":
        read_opt_value(config, argv, ++i, CNF_STDOUT_MATCHES, "--out-matches")
      break;
      case "--no-out":
        config[CNF_STDOUT_EQUALS] = ""
      break;
      case "--err-equals":
        read_opt_value(config, argv, ++i, CNF_STDERR_EQUALS, "--err-equals")
      break;
      case "--err-equals-file":
        read_opt_value(config, argv, ++i, CNF_STDERR_EQUALS_FILE, "--err-equals-file")
      break;
      case "--err-matches":
        read_opt_value(config, argv, ++i, CNF_STDERR_MATCHES, "--err-matches")
      break;
      case "--no-err":
        config[CNF_STDERR_EQUALS] = ""
      break;
      case "--running":
        read_opt_value(config, argv, ++i, CNF_COMMAND, "--running")
        prepare_exec(config[CNF_COMMAND], config_running_exec)
      break;
      case "--forward-in":
        # Override the default to do nothing
        config[CNF_FORWARD_IN] = ""
      break;
      default:
        print "Unknown option "argv[i]" given" > "/dev/stderr"
        usage()
      break;
    }
  }

  # Print config
  # for (key in config) {
  #   print key": "config[key]
  # }

  if (!(CNF_COMMAND in config)) {
    print "No operation specified. Use --running" > "/dev/stderr"
    usage()
  }
}

function read_opt_value(config, argv, value_index, cnf_name, arg_name) {
  if (length(argv) <= value_index) {
    print "Option "arg_name" given with no value" > "/dev/stderr"
    usage()
  }
  config[cnf_name] = argv[value_index]
}

# Turn the command specifier into a shell call to run and capture stuff
function prepare_exec(command_specifier, exec_array) {
  new_array(exec_array)
  token = "assert-exec-"(1000000*rand())
  exec_array["stdout"] = "/tmp/assert-exec-"token"-stdout"
  exec_array["stderr"] = "/tmp/assert-exec-"token"-stderr"

  COMMA_SUBSTITUTE="string_never_to_be_found_in_real_input_to_be_used_to_temporarily_hide_quoted_commas_before_splitting"
  gsub(/\\,/, COMMA_SUBSTITUTE, command_specifier)
  split(command_specifier, chunks, ",")

  command = "exec"
  show_command = ""
  for(ii = 1; ii <= length(chunks); ii++) {
    arg = chunks[ii]
    gsub(COMMA_SUBSTITUTE, ",", arg)
    gsub(/[']/, "'\\''", arg)
    command = command" '"arg"'"
    show_command = show_command"'"arg"' "
  }
  command = command" > '"exec_array["stdout"]"' 2> '"exec_array["stderr"]"'"
  exec_array["command"] = command
  gsub(/[ ]$/, "", show_command) # Remove trailing space
  exec_array["show_command"] = show_command
}

function new_array(array) {
  split("", array)
}

function push(array, element) {
  array[length(array)+1] = element
}

BEGIN {
  EXIT_SUCCESS = 0
  EXIT_USAGE = 1
  EXIT_FAILURE = 2

  CNF_MESSAGE="message"
  CNF_STDOUT_EQUALS="stdout-equals"
  CNF_STDOUT_EQUALS_FILE="stdout-equals-file"
  CNF_STDOUT_MATCHES="stdout-matches"
  CNF_STDERR_EQUALS="stderr-equals"
  CNF_STDERR_EQUALS_FILE="stderr-equals-file"
  CNF_STDERR_MATCHES="stderr-matches"
  CNF_EXIT="exit"
  CNF_COMMAND="command"
  CNF_FORWARD_IN="forward-in"

  # We use rand() down the path so initializing it now. Second resolution of systime()
  # is not sufficient to prevent consecutive invocations of assert to clash on random paths generated
  srand(systime()*PROCINFO["pid"])
  RS="^$" # So we can read whole files easily

  # Tool configuration provided
  new_array(config)
  # ExecArray for main execution task (--running)
  new_array(config_running_exec)
  # Map of problems detected
  new_array(assertions)

  init_opts(config, ARGC, ARGV)

  config_running_exec["command"] = config[CNF_FORWARD_IN]""config_running_exec["command"]

#print config_running_exec["command"]
  exit_code = system(config_running_exec["command"])

  verify_outputs(exit_code)
  report_results()
}

function verify_outputs(exit_code) {
  if (CNF_EXIT in config && exit_code != config[CNF_EXIT]) {
    push(assertions, "Exit code mismatch. Expected "config[CNF_EXIT]", was "exit_code)
  }

  verify_stream("stdout", config_running_exec["stdout"], CNF_STDOUT_MATCHES, CNF_STDOUT_EQUALS, CNF_STDOUT_EQUALS_FILE)
  verify_stream("stderr", config_running_exec["stderr"], CNF_STDERR_MATCHES, CNF_STDERR_EQUALS, CNF_STDERR_EQUALS_FILE)
}

function verify_stream(label, actual_file, K_MATCHES, K_EQUALS, K_EQUALS_FILE) {
  if (K_EQUALS_FILE in config) {
    file_diff = ""
    diff_command(config[K_EQUALS_FILE], actual_file) | getline file_diff
    if (file_diff != "") {
      push(assertions, label" mismatch:\n"file_diff)
    }
  }

  if (K_EQUALS in config && config[K_EQUALS] != "") {
    new_diff_file="/tmp/assert-equals-"(1000000*rand())
    printf "%s",config[K_EQUALS] > new_diff_file
    close(new_diff_file) # Enforce writing the content even when not ending with newline

    literal_diff = ""
    diff_command(new_diff_file, actual_file) | getline literal_diff
    system("rm -f '"new_diff_file"'")
    if (literal_diff != "") {
      push(assertions, label" mismatch (-expected, +actual):\n"literal_diff)
    }
  }

  actual_out = ""
  getline actual_out < (actual_file)
  if (K_EQUALS in config && config[K_EQUALS] == "" && actual_out != "") {
    push(assertions, "Empty "label" expected, was:\n"gensub(/^/, "    ", "g", actual_out))
  }

  if (K_MATCHES in config) {
    if (K_MATCHES in config && actual_out !~ config[K_MATCHES]) {
      push(assertions, "Expected "label" matching '"config[K_MATCHES]"', was:\n    "actual_out)
    }
  }
  system("rm -f '"actual_file"'")
}

function diff_command(expected, actual) {
  return "diff -U10 '"expected"' '"actual"' | sed -e '1,3d' -e '4,$s/^/    /'"
}

function report_results() {
  if (length(assertions) == 0) exit EXIT_SUCCESS

  print "ASSERT: Failed running: "config_running_exec["show_command"]  > "/dev/stderr"
  for (key in assertions) {
    print "  - "assertions[key] > "/dev/stderr"
  }

  exit EXIT_FAILURE
}
